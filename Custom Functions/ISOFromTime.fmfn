/*******************************************************************************
 * ISOFromTime ( theTime )
 *
 * @parameter theTime: The FileMaker time to convert to ISO 8601 format
 *
 * @return theTime rendered in ISO 8601 format: hh:mm:ss,sss (24-hour time)
 *
 * @history 2017-04-16 - Jeremy Bante <http://scr.im/fugue> - Created
 * @history 2017-05-11 - Jeremy Bante <http://scr.im/fugue> - Improving response
 * to error conditions.
 *
 * @see http://en.wikipedia.org/wiki/ISO_8601
 ******************************************************************************/

Let ( [
	_time =
		If ( theTime ≥ Time ( 24 ; 0 ; 0 ) ;
			GetAsTimestamp ( theTime ) ;
			/* Else */ theTime
		) ;
	_seconds = Seconds ( _time ) ;
	_integerSeconds = Div ( _seconds ; 1 ) ;
	_fractionSeconds = Mod ( _seconds ; 1 )
] ;
	Case (
		IsEmpty ( theTime ) ;
			"" ;

		_seconds = "?" ;
			"? Value is not a time¶  " & theTime ;

		/* Else */
			Right ( "00" & Hour ( _time ) ; 2 )
			& ":" & Right ( "00" & Minute ( _time ) ; 2 )
			& If ( _seconds > 0 ;
				":" & Right ( "00" & _integerSeconds ; 2 )
			)
			& If ( _fractionSeconds > 0 ;
				"," & Replace ( _fractionSeconds ; 1 ; 1 ; "" )
			)
	)
)